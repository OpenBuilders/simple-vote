#include "imports/common/stdlib.fc";
#include "imports/vote/vote_constants.fc";
#include "imports/vote/vote_utils.fc";

const int const::min_to_vote = 1000000000; ;; 1 TON
const int const::min_for_storage = 60000000; ;; 0.06
const int const::min_for_deploy_vote = 10000000; ;; 0.01

;; storage variables
global int storage::votes_yes;
global int storage::votes_no;
global int storage::votes_abstain;
global int storage::time_when_finish;
global slice storage::initiator_addr;
global cell storage::vote_code_hex;
global cell storage::project_name;

() load_data() impure {
    var ds = get_data().begin_parse();

    storage::votes_yes = ds~load_uint(32);
    storage::votes_no = ds~load_uint(32);
    storage::votes_abstain = ds~load_uint(32);
    storage::time_when_finish = ds~load_int(32);
    storage::initiator_addr = ds~load_msg_addr();
    storage::vote_code_hex = ds~load_ref();
    storage::project_name = ds~load_ref();

    ds.end_parse();
}

() save_data() impure {
    set_data(
            begin_cell()
                    .store_uint(storage::votes_yes, 32)
                    .store_uint(storage::votes_no, 32)
                    .store_uint(storage::votes_abstain, 32)
                    .store_int(storage::time_when_finish, 32)
                    .store_slice(storage::initiator_addr)
                    .store_ref(storage::vote_code_hex)
                    .store_ref(storage::project_name)
                    .end_cell()
    );
}

;; 'Already voted'
builder store_voted_text(builder b) inline {
    return b.store_uint(5183393240786912077281565631844, 104);
}

() deploy_vote_msg(slice to_address, int vote, int msg_value) impure {
    cell data = pack_data(to_address, my_address(), -2);
    cell state_init = calculate_state_init(data, storage::vote_code_hex);
    slice to_wallet_address = calculate_address(state_init);
    cell body = begin_cell()
            .store_int(vote, 3)
            .store_slice(to_address)
            .end_cell();
    var msg = begin_cell()
            .store_uint(0x18, 6)
            .store_slice(to_wallet_address)
            .store_coins(const::min_for_deploy_vote)
            .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
            .store_ref(state_init)
            .store_ref(body)
            ;

    send_raw_message(msg.end_cell(), 1);
}

() decrease_vote(int vote) impure {
    if (vote == const::vote:yes) {
        storage::votes_yes -= 1;
    } elseif (vote == const::vote:no) {
        storage::votes_no -= 1;
    } else {
        storage::votes_abstain -= 1;
    }
}

() increase_vote(int vote) impure {
    if (vote == const::vote:yes) {
        storage::votes_yes += 1;
    } elseif (vote == const::vote:no) {
        storage::votes_no += 1;
    } else {
        storage::votes_abstain += 1;
    }
}

() on_bounce(int msg_value, slice in_msg_body) impure {
    in_msg_body~skip_bits(32);
    load_data();
    int vote = in_msg_body~load_int(3);
    decrease_vote(vote);
    save_data();
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore all empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; all bounced messages
        on_bounce(msg_value, in_msg_body);
        return ();
    }
    throw_unless(error::not_enough_funds, msg_value >= const::min_to_vote);
    load_data();
    throw_if(error::voting_finished, (storage::time_when_finish > 0) & (storage::time_when_finish > now())); ;; time is over

    slice from_addr = cs~load_msg_addr();
    int op = in_msg_body~load_uint(32);


    if (equal_slices(storage::initiator_addr, from_addr)) {
        if (op == op::take_commission) {
            int commission = my_balance - const::min_for_storage;
            send_text_msg(from_addr, commission, 1, begin_cell().store_uint(8390880524967965285, 64));
            return ();
        }
    }

    ;; new vote
    if (op == 0) {
        int vote = parse_yes_or_not_msg(in_msg_body);
        increase_vote(vote);
        save_data();
        if (const::min_for_storage > my_balance) {
            msg_value -= const::min_for_storage;
        }
        deploy_vote_msg(from_addr, vote, msg_value);
        return ();
    }

    ;; change vote
    if (op == op::change_vote) {
        slice u_addr = in_msg_body~load_msg_addr();
        slice vote_addr = calculate_vote_address(u_addr, my_address(), storage::vote_code_hex);
        if (equal_slices(vote_addr, from_addr)) {
            int prev_vote = in_msg_body~load_int(3);
            int new_vote = in_msg_body~load_int(3);
            decrease_vote(prev_vote);
            increase_vote(new_vote);
            save_data();
            return ();
        }
        throw(error::invalid_address);
    }

    throw(error:unknown_op);
}

;; get votes
(int, int) get_votes() method_id {
    load_data();
    return (storage::votes_yes, storage::votes_no);
}

(int) is_finished() method_id {
    load_data();
    return (storage::time_when_finish > 0) & (storage::time_when_finish > now());
}

(slice) get_vote_addr(slice user_addr) method_id {
    load_data();
    return calculate_vote_address(user_addr, my_address(), storage::vote_code_hex);
}

(slice) get_project_name() method_id {
    load_data();
    return storage::project_name.begin_parse();
}
